// Inspired by rayronvictor's PR #248 tp react-native-config
// https://github.com/luggit/react-native-config/pull/248

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
  repositories {
    maven {
      url 'https://maven.google.com/'
      name 'Google'
    }
    jcenter()
    google()
  }

  def buildGradleVersion = ext.has('buildGradlePluginVersion') ? ext.get('buildGradlePluginVersion') : '3.4.0'

  dependencies {
    classpath "com.android.tools.build:gradle:$buildGradleVersion"
  }
}

apply plugin: 'com.android.library'

def _reactNativeVersion = safeExtGet('reactNative', '+')
def _compileSdkVersion = safeExtGet('compileSdkVersion', 27)
def _buildToolsVersion = safeExtGet('buildToolsVersion', '27.0.3')
def _minSdkVersion = safeExtGet('minSdkVersion', 16)
def _targetSdkVersion = safeExtGet('targetSdkVersion', 27)

android {
  compileSdkVersion _compileSdkVersion
  buildToolsVersion _buildToolsVersion

  defaultConfig {
    minSdkVersion _minSdkVersion
    targetSdkVersion _targetSdkVersion
    versionCode 1
    versionName "1.0"
  }
  lintOptions {
    abortOnError false
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation "com.facebook.react:react-native:${_reactNativeVersion}"
}
